взяти визначення РМв1;

зовнішня дія розширення_мавки_привіт_сума(а: ціле, б: ціле) -> ціле;

зовнішня дія завантажити_РМв1(Р: адреса<РМв1::Розширення>) {
  змінна сума = розширення_мавки_привіт_сума(2, 3);
  
  змінна сума_число = РМв1::створити_число(Р, р64(сума)); // може впасти
  якщо РМв1::перевірити_чи_стан_падіння(Р) {
    вернути;
  }
  // очищувач мавки може запуститись в наступній дії "створити_текст" 
  // і знищити предмет з "сума_число", тому нам треба вказати, що він нам ще потрібен
  // притримавши його в глобальному притримувачі (або в звичайному)
  змінна притримане_число = РМв1::притримати_глобально(Р, сума_число);

  змінна результат_текст = РМв1::створити_текст(Р, "результат"); // може впасти
  якщо РМв1::перевірити_чи_стан_падіння(Р) {
    // нам необхідно відпустити глобально притриманий предмет з "сума_число"
    // інакше він існуватиме вічно
    РМв1::відпустити_глобально(Р, притримане_число);
    вернути;
  }
  
  РМв1::предмет_змінити_властивість(
    Р, 
    Р.предмет_модуля, 
    результат_текст,
    сума_число,
    РМв1::Місцезнаходження { пусто, 0 }
  ); // може впасти
  // нам необхідно відпустити глобально притриманий предмет з "сума_число"
  // інакше він існуватиме вічно
  РМв1::відпустити_глобально(Р, притримане_число);
  якщо РМв1::перевірити_чи_стан_падіння(Р) {
    вернути;
  }

  РМв1::надрукувати(Р, "привіт!");
}